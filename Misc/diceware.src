Word_List = get_shell.host_computer.File("/lib/EFFwordlist.txt")
if not Word_List then exit("Error: EFFwordlist.txt not in /lib/ folder")
Word_List = Word_List.get_content.split(char(10))

entropy_calc = function(words)
	combos = 7776^words
	result=""
	for i in range(1,3)
		cnt=0
		while combos >= 2
			combos = combos/2
			cnt=cnt+1
		end while
		result = result + str(cnt)
		if i == 1 then result = result + "."
		combos = combos^10
	end for
	return result
end function

index_parse = function(die_index)
	table="123456"
	for c in die_index
		if table.indexOf(c) == null then return null
	end for
	
	die_index = die_index.values
	die_index.reverse
	die_index = die_index.join("")
	
	result=0
	for i in die_index.indexes
		result = result + (die_index[i].to_int-1)*6^i
	end for
	return result
end function

pass_gen_prng = function(words)
	clear_screen
	
	passphrase = ""
	for i in range(1,words)
		passphrase = passphrase + Word_List[floor(rnd*7776)] + " "
	end for
	
	user_input("\n<color=white>Generated passphrase: "+passphrase.trim+"\n<color=white>Bits of entropy: "+entropy_calc(words)+"\n\n<color=white>Press enter to return. ")
end function

pass_gen_dice = function(words)
	table = "123456"
	passphrase = ""
	
	for i in range(1,words)
		while true
			clear_screen
			print("\n<color=white>Please roll a die 5 times and record its results in the format [12345] where each digit is a roll result.\n")
			input = user_input("<color=white>Enter results for word "+i+": ")
			
			if input.len != 5 then
				print("<color=red>Invalid input.")
				wait(2)
			else
				index = index_parse(input)
				if typeof(index) != "number" then
					print("<color=red>Invalid input.")
					wait(2)
					continue
				end if
				passphrase = passphrase + Word_List[index] + " "
				break
			end if
		end while
	end for
	
	clear_screen
	user_input("\n<color=white>Generated passphrase: "+passphrase.trim+"\n<color=white>Bits of entropy: "+entropy_calc(words)+"\n\n<color=white>Press enter to return. ")
end function

pass_gen_menu = function()
	words=0
	while words <= 0 or words > 79
		clear_screen
		input = user_input("\n<color=white>How long, in words, do you want your passphrase to be?\n\n<color=white>#> ").to_int
		
		if typeof(input) == "number" then words = input
		
		if words <= 0 or words > 79 then
			print("<color=red>Invalid input.")
			wait(2)
		end if
		
	end while
	
	while true
		clear_screen
		input = user_input("\n<color=white>1) Generate pseudo-random passphrase\n<color=white>2) Generate dice-rolled passphrase\n\n<color=white>#> ")
	
		if input == "1" then
			pass_gen_prng(words)
			return
		else if input == "2" then
			pass_gen_dice(words)
			return
		else
			print("<color=red>Invalid selection.")
			wait(2)
		end if
	
	end while
end function

show_about = function()
	clear_screen
	print("\n<color=white>Diceware is a program that generates passphrases (a password comprised of multiple English words) from a 7,776 word wordlist.")
	print("<color=white>Passphrases are an alternative to passwords that lend themselves to being easier to remember while still being hard to crack.")
	print("<color=white>It is recommended that at least 5 words are used in a passphrase if you are using the 7,776 word list.")
	print("<color=white>It is also recommended to generate passphrases using real life dice.")
	print("<color=white>This makes sure maximum randomness is used to generate the passphrase.")
	user_input("\n<color=white>Press enter to return. ")
end function

main = function()
	while true
		clear_screen
		print("\n<color=red>	 _____  <color=white> _")
		print("<color=red>	|  __ \ <color=white>(_)")
		print("<color=red>	| |  | |<color=white> _ <color=red>  ___ <color=white> ___ <color=red>__      __<color=white> __ _ <color=red> _ __ <color=white> ___")
		print("<color=red>	| |  | |<color=white>| |<color=red> / __|<color=white>/ _ \<color=red>\ \ /\ / /<color=white>/ _` |<color=red>| '__|<color=white>/ _ \")
		print("<color=red>	| |__| |<color=white>| |<color=red>| (__<color=white>|  __/<color=red> \ V  V /<color=white>| (_| |<color=red>| |  <color=white>|  __/")
		print("<color=red>	|_____/ <color=white>|_|<color=red> \___|<color=white>\___|<color=red>  \_/\_/ <color=white> \__,_|<color=red>|_|  <color=white> \___|")
		print("<color=red>   =================================================\n")
		
		input = user_input("<color=white>1) About\n<color=white>2) Generate passphrase\n<color=white>0) Exit\n\n<color=white>#> ")
		
		if input == "1" then
			show_about()
		else if input == "2" then
			pass_gen_menu()
		else if input == "0" then
			clear_screen
			return
		else
			print("<color=red>Invalid selection.")
			wait(2)
		end if
		
	end while
end function


CMD_List = "List of switches for diceware:\n\n-w [num of words]\n"+char(9)+"Specifies how many words the user would like generated in their passphrase. Use in conjuction with -r or -d.\n\n-r\n"+char(9)+"Generates a pseudo-random passphrase.\n\n-d\n"+char(9)+"Generates a passphrase based on the results of rolled dice by the user.\n"

if params.len > 0 then
	if params.len != 3 then exit(CMD_List)
	
	Switches = {}
	for i in params.indexes
		if params[i] == "-w" then
			Switches["-w"] = i
			break
		end if
	end for
	if not Switches.hasIndex("-w") then exit(CMD_List)
	if (typeof(params.indexOf("-r")) == "null") and (typeof(params.indexOf("-d")) == "null") then exit(CMD_List)
	if (typeof(params.indexOf("-r")) != "null") and (typeof(params.indexOf("-d")) != "null") then exit(CMD_List)
	
	Words = params[Switches["-w"]+1].to_int
	if typeof(Words) != "number" then exit(CMD_List)
	if Words <= 0 or Words > 79 then exit(CMD_List)
	
	if typeof(params.indexOf("-r")) != "null" then
		pass_gen_prng(Words)
	else if typeof(params.indexOf("-d")) != "null" then
		pass_gen_dice(Words)
	else
		exit(CMD_List)
	end if
else
	main()
end if
